      SUBROUTINE VLINT(XVECI,YVECI,NDIMI,XVECO,YVECO,NDIMO)
C
C**********************************************************
C
C  NAME- VLINT  LANGUAGE-  FORTRAN 77  TYPE- SUBROUTINE
C
C  VERSION- 1.1  DATE- 10/9/92  PROGRAMMER- P.ROSENKRANZ
C
C  FUNCTION - INTERPOLATION OF YVECI TO YVECO, LINEARLY
C   WITH XVECO IN XVECI
C
C  CALLING SEQUENCE PARAMETERS-
C
C  NAME     TYPE    I/O     DESCRIPTION
C
C  XVECI     R*4     I      ABSCISSA OF INPUT FUNCTION (VALUES MUST
C                           BE INCREASING)
C  YVECI     R*4     I      ORDINATE OF INPUT FUNCTION
C  NDIMI     I*4     I      LENGTH OF XVECI AND YVECI
C  XVECO     R*4     I      ABSCISSA OF OUTPUT FUNCTION (VALUES MUST
C                           BE INCREASING)
C  YVECO     R*4     O      ORDINATE OF OUTPUT FUNCTION
C  NDIMO     I*4     I      LENGTH OF XVECO AND YVECO
C
C********************************************************************
C
      REAL XVECI(NDIMI),YVECI(NDIMI),XVECO(NDIMO),YVECO(NDIMO)
      JJ = 2
      DO 20 I=1,NDIMO
      DO 10 J=JJ,NDIMI
      J1 = J-1
      IF(XVECO(I).LT.XVECI(J)) GOTO 15
10    CONTINUE
15    JJ = J1+1
      D = (XVECO(I)-XVECI(J1))/(XVECI(JJ)-XVECI(J1))
      YVECO(I) = YVECI(J1)*(1.-D) + YVECI(JJ)*D
20    CONTINUE
      RETURN
      END
