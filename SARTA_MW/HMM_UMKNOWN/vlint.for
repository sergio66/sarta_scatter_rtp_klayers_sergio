C  MASSACHUSETTS INSTITUTE OF TECHNOLOGY
C
C  AIRS
C
C  MICROWAVE FIRST-GUESS RETRIEVAL
C
!ROUTINE NAME: VLINT
!CALL INTERFACE:
      SUBROUTINE VLINT(XVECI,YVECI,NDIMI,XVECO,YVECO,NDIMO)
C
!F77  LANGUAGE-  FORTRAN 77
C
!ABSTRACT: INTERPOLATION OF YVECI TO YVECO, LINEARLY
C   WITH XVECO IN XVECI
C
!ROUTINE HISTORY:
C  VERSION- 1.1  DATE- 10/9/92  PROGRAMMER- P.ROSENKRANZ
C
!ARGUMENTS:
C  SPECIFICATIONS-
      IMPLICIT NONE
      INTEGER NDIMI,NDIMO
      REAL XVECI(NDIMI),YVECI(NDIMI),XVECO(NDIMO),YVECO(NDIMO)
      
C  NAME      I/O     DESCRIPTION
C
C  XVECI      I      ABSCISSA OF INPUT FUNCTION 
C  YVECI      I      ORDINATE OF INPUT FUNCTION
C  NDIMI      I      LENGTH OF XVECI AND YVECI
C  XVECO      I      ABSCISSA OF OUTPUT FUNCTION 
C  YVECO      O      ORDINATE OF OUTPUT FUNCTION
C  NDIMO      I      LENGTH OF XVECO AND YVECO
C
!ROUTINES CALLED:none
!PARENT: general purpose
!RETURN VALUES:
!FILES ACCESSED:none
!DESCRIPTION:
!KNOWN BUGS AND LIMITATIONS: VALUES OF XVECI AND XVECO MUST INCREASE
C                            MONOTONICALLY.
!END HEADER*************************************************************
C  LOCAL VARIABLES:
      INTEGER JJ,I,J,J1
      REAL D
C
      JJ = 2
      DO 20 I=1,NDIMO
      DO 10 J=JJ,NDIMI
      J1 = J-1
      IF(XVECO(I).LT.XVECI(J)) GOTO 15
10    CONTINUE
15    JJ = J1+1
      D = (XVECO(I)-XVECI(J1))/(XVECI(JJ)-XVECI(J1))
      YVECO(I) = YVECI(J1)*(1.-D) + YVECI(JJ)*D
20    CONTINUE
      RETURN
      END
